---
title: Terraform. Развертывание внешнего и внутреннего веб-приложения, безопасно подключенных с помощью интеграции с виртуальной сетью и частной конечной точки
description: Узнайте, как использовать поставщик Terraform для Службы приложений, чтобы развертывать два веб-приложения, безопасно подключенных с помощью частной конечной точки и интеграции с виртуальной сетью
author: ericgre
ms.assetid: 3e5d1bbd-5581-40cc-8f65-bc74f1802156
ms.topic: sample
ms.date: 08/10/2020
ms.author: ericg
ms.service: app-service
ms.workload: web
ms.openlocfilehash: 238cd5e79fe749052206cfdf25d576f88f1020e8
ms.sourcegitcommit: d8b8768d62672e9c287a04f2578383d0eb857950
ms.translationtype: HT
ms.contentlocale: ru-RU
ms.lasthandoff: 08/11/2020
ms.locfileid: "88065443"
---
# <a name="create-two-web-apps-connected-securely-with-private-endpoint-and-vnet-integration"></a>Создание безопасного подключения двух веб-приложений с помощью частной конечной точки и интеграции с виртуальной сетью

В этой статье показано, как использовать [частную конечную точку](https://docs.microsoft.com/azure/app-service/networking/private-endpoint) и [интеграцию с локальной виртуальной сетью](https://docs.microsoft.com/azure/app-service/web-sites-integrate-with-vnet) для безопасного подключения двух веб-приложений (внешнего и внутреннего) с помощью следующих действий:
- Развертывание виртуальной сети
- Создание первой подсети для интеграции
- Создание второй подсети для частной конечной точки и задание конкретного параметра для отключения сетевых политик
- Развертывание одного плана Службы приложений категории "Премиум 2", минимального SKU для функции "Частная конечная точка"
- Создание внешнего веб-приложения с конкретными параметрами приложения для использования частной зоны DNS ([больше](https://docs.microsoft.com/azure/app-service/web-sites-integrate-with-vnet#azure-dns-private-zones))
- Подключение внешнего веб-приложения к подсети интеграции
- Создание внутреннего веб-приложения
- Создание частной зоны DNS с именем зоны приватного канала для веб-приложения privatelink.azurewebsites.net
- Связывание этой зоны с виртуальной сетью
- Создание частной конечной точки для внутреннего веб-приложения в подсети конечной точки и регистрация имен DNS (веб-сайт и SCM) в ранее созданной частной зоне DNS

## <a name="how-to-use-terraform-in-azure"></a>Как использовать Terraform в Azure

Перейдите к [документации по Azure](https://docs.microsoft.com/azure/developer/terraform/), чтобы получить сведения об использовании Terraform в Azure.

## <a name="the-complete-terraform-file"></a>Полный файл Terraform

Чтобы использовать этот файл, необходимо изменить свойство name для ресурсов frontwebapp и backwebapp (имя webapp должно быть глобально уникальным DNS-именем). 

```hcl
provider "azurerm" {
  version = "~>2.0"
  features {}
}

resource "azurerm_resource_group" "rg" {
  name     = "appservice-rg"
  location = "francecentral"
}

resource "azurerm_virtual_network" "vnet" {
  name                = "vnet"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  address_space       = ["10.0.0.0/16"]
}

resource "azurerm_subnet" "integrationsubnet" {
  name                 = "integrationsubnet"
  resource_group_name  = azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.0.1.0/24"]
  delegation {
    name = "delegation"
    service_delegation {
      name = "Microsoft.Web/serverFarms"
    }
  }
}

resource "azurerm_subnet" "endpointsubnet" {
  name                 = "endpointsubnet"
  resource_group_name  = azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.0.2.0/24"]
  enforce_private_link_endpoint_network_policies = true
}

resource "azurerm_app_service_plan" "appserviceplan" {
  name                = "appserviceplan"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  sku {
    tier = "Premiumv2"
    size = "P1v2"
  }
}

resource "azurerm_app_service" "frontwebapp" {
  name                = "frontwebapp20200810"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  app_service_plan_id = azurerm_app_service_plan.appserviceplan.id

  app_settings = {
    "WEBSITE_DNS_SERVER": "168.63.129.16",
    "WEBSITE_VNET_ROUTE_ALL": "1"
  }
}

resource "azurerm_app_service_virtual_network_swift_connection" "vnetintegrationconnection" {
  app_service_id  = azurerm_app_service.frontwebapp.id
  subnet_id       = azurerm_subnet.integrationsubnet.id
}

resource "azurerm_app_service" "backwebapp" {
  name                = "backwebapp20200810"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  app_service_plan_id = azurerm_app_service_plan.appserviceplan.id
}

resource "azurerm_private_dns_zone" "dnsprivatezone" {
  name                = "privatelink.azurewebsites.net"
  resource_group_name = azurerm_resource_group.rg.name
}

resource "azurerm_private_dns_zone_virtual_network_link" "dnszonelink" {
  name = "dnszonelink"
  resource_group_name = azurerm_resource_group.rg.name
  private_dns_zone_name = azurerm_private_dns_zone.dnsprivatezone.name
  virtual_network_id = azurerm_virtual_network.vnet.id
}

resource "azurerm_private_endpoint" "privateendpoint" {
  name                = "backwebappprivateendpoint"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  subnet_id           = azurerm_subnet.endpointsubnet.id

  private_dns_zone_group {
    name = "privatednszonegroup"
    private_dns_zone_ids = [azurerm_private_dns_zone.dnsprivatezone.id]
  }

  private_service_connection {
    name = "privateendpointconnection"
    private_connection_resource_id = azurerm_app_service.backwebapp.id
    subresource_names = ["sites"]
    is_manual_connection = false
  }
}
```




## <a name="next-steps"></a>Дальнейшие действия


> [Документация по Terraform в Azure](https://docs.microsoft.com/azure/developer/terraform/)
